# $Id: PKGBUILD 75947 2012-09-06 19:45:18Z jlichtblau $
# Upstream Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Fantix King <fantix.king at gmail.com>

_basepkgname=geos
pkgname=libx32-geos
pkgver=3.3.5
pkgrel=1.1
pkgdesc="C++ port of the Java Topology Suite (x32 ABI)"
arch=('x86_64')
url="http://trac.osgeo.org/geos/"
license=('LGPL')
depends=('libx32-gcc-libs' 'bash' "${_basepkgname}=${pkgver}")
makedepends=('gcc-multilib-x32')
options=('!libtool' '!emptydirs')
changelog=$_basepkgname.changelog
source=(http://download.osgeo.org/$_basepkgname/$_basepkgname-$pkgver.tar.bz2)
sha256sums=('3b513fbe2d155364d61e76d9c250d6d8e75b5166783a233596c744373cb5874f')

build() {
  cd ${srcdir}/$_basepkgname-$pkgver

  export CC="gcc -mx32"
  export CXX="g++ -mx32"

  ./configure --prefix=/usr --libdir=/usr/libx32
  make
}

package() {
  cd ${srcdir}/$_basepkgname-$pkgver

  make DESTDIR=${pkgdir} install

  mv "${pkgdir}/usr/include/geos/platform.h" "${pkgdir}"
  rm -rf "${pkgdir}/usr/include"
  install -dm755 "${pkgdir}/usr/include/geos"
  mv "${pkgdir}/platform.h" "${pkgdir}/usr/include/geos/platform-x32.h"
  mv "${pkgdir}/usr/bin/geos-config" "${pkgdir}/usr/bin/geos-x32-config"
}
