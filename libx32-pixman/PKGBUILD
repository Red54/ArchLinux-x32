# $Id: PKGBUILD 73349 2012-07-06 15:36:55Z bluewind $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
_pkgbasename=pixman
pkgname=libx32-$_pkgbasename
pkgver=0.26.2
pkgrel=1.1
pkgdesc="Pixman library (x32 ABI)"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('libx32-glibc' $_pkgbasename)
makedepends=('gcc-multilib-x32')
options=('!libtool')
source=(http://xorg.freedesktop.org/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.bz2)
sha1sums=('1daf014714cc29147726abada12359345d156705')

build() {
  export CC="gcc -mx32"
  export CXX="g++ -mx32"
  export PKG_CONFIG_PATH="/usr/libx32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/libx32 --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
