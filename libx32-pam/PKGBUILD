# Maintainer: josephgbr <rafael.f.f1 at gmail.com> 

_pkgbase=pam
pkgname=lib32-${_pkgbase}
pkgver=1.1.5
pkgrel=5
pkgdesc="PAM (Pluggable Authentication Modules) library (32 bit)"
arch=('x86_64')
license=('GPL2')
url="http://www.kernel.org/pub/linux/libs/pam/"
depends=('lib32-glibc' 'lib32-libtirpc' 'lib32-db' 'lib32-cracklib' "${_pkgbase}")
makedepends=('lib32-flex' 'gcc-multilib')
options=('!libtool' '!emptydirs')
source=(https://fedorahosted.org/releases/l/i/linux-pam/Linux-PAM-${pkgver}.tar.bz2
        ftp://ftp.suse.com/pub/people/kukuk/pam/pam_unix2/pam_unix2-2.6.tar.bz2
        pam-glibc-2.16.patch
        __libc_lock_define_initialized.patch)
md5sums=('927ee5585bdec5256c75117e9348aa47'
         'e2788389a6c59224110a45fcff30e02b'
         '469023948632df30d2cb2015b77aa0fe'
         '7d3c716fb7b0136e10abf57392c23bde')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/Linux-PAM-${pkgver}"
  patch -Np1 -i ../pam-glibc-2.16.patch
  ./configure --libdir=/usr/lib32
  make

  cd "${srcdir}/pam_unix2-2.6"
  sed -e 's|pam_syslog (pam_handle_t|pam_syslog (const pam_handle_t|g' \
  	  -i src/public.h src/support.c
  patch -Np1 -i ../__libc_lock_define_initialized.patch
  ./configure --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/Linux-PAM-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  cd "${srcdir}/pam_unix2-2.6"
  #make DESTDIR="${pkgdir}" install
  install src/pam_unix2.so "${pkgdir}/usr/lib32/security/pam_unix2.so"

  # fix some missing symlinks from old pam for compatibility
  cd "${pkgdir}/usr/lib32/security"
  ln -s pam_unix.so pam_unix_acct.so
  ln -s pam_unix.so pam_unix_auth.so
  ln -s pam_unix.so pam_unix_passwd.so
  ln -s pam_unix.so pam_unix_session.so
  
  # cleanup for lib32 package
  rm -rf "${pkgdir}"/{etc,sbin,usr/{include,share}}
}
