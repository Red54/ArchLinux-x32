# $Id: PKGBUILD 164762 2012-08-04 18:49:49Z ronald $
# Upstream Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Maintainer: Fantix King <fantix.king at gmail.com>

_basepkgname=lapack
pkgname=libx32-lapack
pkgver=3.4.1
pkgrel=1.1
url="http://www.netlib.org/lapack"
pkgdesc="Linear Algebra PACKage (x32 ABI)"
makedepends=('gcc-fortran-multilib-x32')
depends=("libx32-blas=${pkgver}" "${_basepkgname}=${pkgver}")
arch=('x86_64')
license=("custom")
source=(http://www.netlib.org/${_basepkgname}/${_basepkgname}-${pkgver}.tgz
        Makefile.lapack) 
sha1sums=('910109a931524f8dcc2734ce23fe927b00ca199f'
          '2bedfe0d778b401b24d1fbf0eb1dc576d374be28')


build() {
  cd "${srcdir}/${_basepkgname}-${pkgver}"
  cp -f INSTALL/make.inc.gfortran make.inc
  cp -f "${srcdir}/Makefile.lapack" SRC/Makefile

  export FC='gfortran -mx32'

  pushd INSTALL
  make clean
  make FORTRAN='gfortran -mx32' LOADER='gfortran -mx32' NOOPT="${CFLAGS/-O?/-O0} -fPIC" OPTS="${CFLAGS} -fPIC"
  popd
  # make sure no files with '_', *vxx.o *vfx.o and *2.O as defined in *ASRC
  #  are in OBJS
  pushd SRC
  make clean
  
  make FFLAGS="${CFLAGS} -fPIC" CFLAGS="${CFLAGS} -fPIC" shared
  cp liblapack.so.${pkgver} "${srcdir}/${_basepkgname}-${pkgver}/"
  popd
}

package() {
  install -m755 -d "${pkgdir}/usr/libx32"
  install -m755 "${srcdir}/${_basepkgname}-${pkgver}/liblapack.so.${pkgver}" \
    "${pkgdir}/usr/libx32/"
  ln -sf liblapack.so.${pkgver} "${pkgdir}/usr/libx32/liblapack.so"
  ln -sf liblapack.so.${pkgver} "${pkgdir}/usr/libx32/liblapack.so.3"

  # install license
  install -dm755 "$pkgdir"/usr/share/licenses
  ln -s ${_basepkgname} "${pkgdir}"/usr/share/licenses/${pkgname}
}

sha1sums=('c115223ac1bac9ab971aae865d3e95442bc979bc'
          '628b8c3a2ef8bbbc67c509fc626d8804b61e0654')
