# $Id: PKGBUILD 62807 2012-01-27 07:09:41Z ebelanger $
# Upstream Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
# Maintainer: Fantix King <fantix.king at gmail.com>

_basepkgname=libgeotiff
pkgname=libx32-libgeotiff
pkgver=1.3.0
pkgrel=2.1
pkgdesc="A TIFF based interchange format for georeferenced raster imagery (x32 ABI)"
arch=('x86_64')
license=('custom')
url="http://www.remotesensing.org/geotiff"
depends=('libx32-libtiff' 'libx32-proj' 'libx32-libjpeg>=7' "${_basepkgname}=${pkgver}")
makedepends=('gcc-multilib-x32')
options=('!makeflags' '!libtool')
source=(ftp://ftp.remotesensing.org/pub/geotiff/libgeotiff/$_basepkgname-$pkgver.tar.gz)
md5sums=('6ac3c22e44711e9a3add9044e40b9527')

build() {
	cd "$srcdir/$_basepkgname-$pkgver"

  export CC="gcc -mx32"
  export CXX="g++ -mx32"
  export PKG_CONFIG_PATH="/usr/libx32/pkgconfig"

	./configure --prefix=/usr --libdir=/usr/libx32
 	make
}

package() {
	cd "$srcdir/$_basepkgname-$pkgver"
	make DESTDIR="$pkgdir" install

    rm -rf "${pkgdir}"/usr/{include,share,bin}

  # install license
  install -dm755 "$pkgdir"/usr/share/licenses
  ln -s ${_basepkgname} "${pkgdir}"/usr/share/licenses/${pkgname}
}
