# $Id: PKGBUILD 164248 2012-07-28 09:51:48Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=binx32-tk
pkgver=8.5.12
pkgrel=1.1
pkgdesc="A windowing toolkit for use with tcl (x32 ABI)"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tk=${pkgver}" "binx32-tcl=${pkgver}" 'libx32-libxss' 'libx32-libxft')
makedepends=("gcc-multilib-x32")
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
md5sums=('a01640176845c1f654a412cfc9bab13b')

build() {
  cd ${srcdir}/tk${pkgver}/unix

  export CC="gcc -mx32"
  export CXX="g++ -mx32"
  export PKG_CONFIG_PATH="/usr/libx32/pkgconfig"

  ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --libdir=/usr/libx32

  # http://cblfs.cross-lfs.org/index.php/Tk
  sed -i 's@/lib/@/libx32/@' Makefile
  
  make
}

#check() {
#  cd ${srcdir}/tk${pkgver}/unix
#  make test
#}

package() {
  cd ${srcdir}/tk${pkgver}/unix

  make INSTALL_ROOT=${pkgdir} install install-private-headers
  mv ${pkgdir}/usr/bin/wish8.5 ${pkgdir}/usr/bin/wish8.5-x32
  ln -sf wish8.5-x32 ${pkgdir}/usr/bin/wish-x32
  
  cd ${srcdir}/tk${pkgver}

  # install license
  install -Dm644 license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces
  sed -i \
    -e "s,^TK_BUILD_LIB_SPEC='-L.*/unix,TK_BUILD_LIB_SPEC='-L/usr/libx32," \
    -e "s,^TK_SRC_DIR='.*',TK_SRC_DIR='/usr/include'," \
    -e "s,^TK_BUILD_STUB_LIB_SPEC='-L.*/unix,TK_BUILD_STUB_LIB_SPEC='-L/usr/libx32," \
    -e "s,^TK_BUILD_STUB_LIB_PATH='.*/unix,TK_BUILD_STUB_LIB_PATH='/usr/libx32," \
    ${pkgdir}/usr/libx32/tkConfig.sh

  # remove duplicate include and manual files
  rm -rf ${pkgdir}/usr/{include,share}
}
