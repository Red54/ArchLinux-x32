# $Id: PKGBUILD 26680 2010-09-15 22:51:35Z bluewind $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

_pkgbasename=libxslt
pkgname=libx32-$_pkgbasename
pkgver=1.1.27
pkgrel=1.1
pkgdesc="XML stylesheet transformation library (x32 ABI)"
arch=('x86_64')
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=('libx32-libxml2>=2.7.7' 'libx32-libgcrypt>=1.4.4' $_pkgbasename)
makedepends=(gcc-multilib-x32)
options=('!libtool')
source=(ftp://xmlsoft.org/libxslt/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('4d01ff464b9ea7236925a1adf147c7f8')

build() {
  export CC="gcc -mx32"
  export CXX="g++ -mx32"
  export PKG_CONFIG_PATH="/usr/libx32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/libx32 --without-python
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
