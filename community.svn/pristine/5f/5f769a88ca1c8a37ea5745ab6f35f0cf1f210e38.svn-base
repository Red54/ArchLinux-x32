# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer: Mikko Seppälä <t-r-a-y@mbnet.fi>
_pkgbasename=atk
pkgname=lib32-$_pkgbasename
pkgver=2.4.0
pkgrel=1
pkgdesc='Library providing a set of interfaces for accessibility (32-bit)'
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2>=2.25.17' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.xz)
url='http://www.gtk.org/'
sha256sums=('091e9ce975a9fbbc7cd8fa64c9c389ffb7fa6cdde58b6d5c01b2c267093d888d')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-introspection
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
