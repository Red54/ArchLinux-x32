# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: qwak <qwak@w8.pl>
# Contributor: ThatGuyJon <theonereddragon@gmail.com>

_pkgname=sdl_mixer
pkgname=lib32-$_pkgname
pkgver=1.2.11
pkgrel=3
pkgdesc="A simple multi-channel audio mixer"
arch=('x86_64')
url="http://www.libsdl.org/projects/SDL_mixer/"
license=('LGPL' 'GPL')
depends=("$_pkgname" 'lib32-sdl>=1.2.12' 'lib32-libvorbis' 'lib32-libmikmod' 'lib32-smpeg')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://www.libsdl.org/projects/SDL_mixer/release/SDL_mixer-${pkgver}.tar.gz")
md5sums=('65ada3d997fe85109191a5fb083f248c')

build() {
  cd "${srcdir}/SDL_mixer-${pkgver}"

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  sed -e "/CONFIG_FILE_ETC/s/\/etc\/timidity.cfg/\/etc\/timidity++\/timidity.cfg/" \
      -e "/DEFAULT_PATH/s/\/etc\/timidity/\/etc\/timidity++/" \
      -e "/DEFAULT_PATH2/s/\/usr\/local\/lib\/timidity/\/usr\/lib\/timidity/" \
      -i timidity/config.h

  ./configure \
	--prefix=/usr \
	--libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/SDL_mixer-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Remove uneeded header files
  rm -rf "$pkgdir/usr/include"
}
